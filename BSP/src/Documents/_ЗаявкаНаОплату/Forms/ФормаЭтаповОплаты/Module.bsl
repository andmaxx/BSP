&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если 
		НЕ Параметры.Свойство("Ссылка") ИЛИ НЕ ЗначениеЗаполнено(Параметры.Ссылка) 
	Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
		
	ДокументСсылка = Параметры.Ссылка;
	СуммаДокумента = ДокументСсылка.СуммаПлатежа;
	ДатаПервойОплаты = ДокументСсылка.ДатаОплаты;
	
КонецПроцедуры



&НаСервере
Процедура СохранитьИЗакрытьНаСервере()
	//СтруктураПараметров = Новый Структура;
	//СтруктураПараметров.Вставить("СоздаватьДокументы", Истина);
	//СтруктураПараметров.Вставить("РазбивкаНаЭтапыОплаты", РазбивкаНаЭтапыОплаты);
	//ЭтаФорма.Закрыть(СтруктураПараметров);	
КонецПроцедуры

&НаКлиенте
Процедура РазбитьНаЭтапыНаКлиенте(ТекущийЭтап = 0)
	
	Если ТекущийЭтап = 0 Тогда
		РазбивкаНаЭтапыОплаты.Очистить();
		ПервыйЭтап = 1;
	Иначе 
		ПервыйЭтап = ТекущийЭтап;	
	КонецЕсли;
	
	СуммаКРаспределению = СуммаДокумента;
	ПроцентКРаспределению = 100;
	
	Если ВариантРазбивкиНаЭтапы = "Процент" Тогда
		ЦелоеЧислоЭтапов = Цел(ПроцентКРаспределению/ПроцентЭтапа);
		КоличествоЭтаповРазбивки = ?(ЦелоеЧислоЭтапов * ПроцентЭтапа = ПроцентКРаспределению, ЦелоеЧислоЭтапов, ЦелоеЧислоЭтапов + 1);
	Иначе
		КоличествоЭтаповРазбивки = КоличествоЭтапов;
	КонецЕсли;
	
	Для Этап = 1 По КоличествоЭтаповРазбивки Цикл
		
		Если ТекущийЭтап = 0 Тогда
			НС = РазбивкаНаЭтапыОплаты.Добавить();
			Если ЗначениеЗаполнено(ДатаПервойОплаты) Тогда
				НС.ДатаОплаты	 = ДобавитьМесяц(ДатаПервойОплаты, Этап - 1);
			КонецЕсли;
			НС.НомерСтроки = Этап;
		Иначе
			НС = РазбивкаНаЭтапыОплаты[Этап - 1];
		КонецЕсли;
		
		Если Этап > ТекущийЭтап Тогда
			//пересчет сумм
			Если ВариантРазбивкиНаЭтапы = "Процент" Тогда
				НС.Процент		 = ПроцентЭтапа;
				НС.Сумма		 = Окр(СуммаДокумента * ПроцентЭтапа / 100, 2);
			Иначе
				НС.Сумма		 = Окр(СуммаКРаспределению / (КоличествоЭтаповРазбивки - ТекущийЭтап), 2);
				НС.Процент		 = НС.Сумма / СуммаДокумента * 100;
			КонецЕсли
		Иначе
			СуммаКРаспределению		 = СуммаКРаспределению - НС.Сумма;
			ПроцентКРаспределению	 = ПроцентКРаспределению - НС.Процент;
		КонецЕсли;
		
		//контроль отрицательных сумм
		СуммаОстаток 	= НС.Сумма + СуммаДокумента - РазбивкаНаЭтапыОплаты.Итог("Сумма");
		ПроцентОстаток	= НС.Процент + 100 - РазбивкаНаЭтапыОплаты.Итог("Процент");
		Если СуммаОстаток < 0 Тогда
			НС.Сумма = НС.Сумма + СуммаОстаток;	
			НС.Процент = НС.Процент + ПроцентОстаток;	
		КонецЕсли;
		
		//корректируем остаток
		Если Этап = КоличествоЭтаповРазбивки Тогда
			СуммаОстаток 	= НС.Сумма + СуммаДокумента - РазбивкаНаЭтапыОплаты.Итог("Сумма");
			ПроцентОстаток	= НС.Процент + 100 - РазбивкаНаЭтапыОплаты.Итог("Процент");
			НС.Сумма = СуммаОстаток;
			НС.Процент = ПроцентОстаток;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СоздаватьДокументы", Истина);
	СтруктураПараметров.Вставить("РазбивкаНаЭтапыОплаты", РазбивкаНаЭтапыОплаты);
	ЭтаФорма.Закрыть(СтруктураПараметров);	
//	СохранитьИЗакрытьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура РазбитьНаЭтапы(Команда)
	РазбитьНаЭтапыНаКлиенте();
КонецПроцедуры


&НаКлиенте
Процедура РазбивкаНаЭтапыОплатыСуммаПриИзмененииНаКлиенте()
    НС = Элементы.РазбивкаНаЭтапыОплаты.ТекущиеДанные;
	НС.Процент	= НС.Сумма / СуммаДокумента * 100;
	РазбитьНаЭтапыНаКлиенте(НС.НомерСтроки);
КонецПроцедуры


&НаКлиенте
Процедура РазбивкаНаЭтапыОплатыСуммаПриИзменении(Элемент)
	РазбивкаНаЭтапыОплатыСуммаПриИзмененииНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ВидимостьВаринатовЭтапов()
	Элементы.КоличествоЭтапов.Видимость = НЕ ВариантРазбивкиНаЭтапы = "Процент"; 
	Элементы.КоличествоЭтапов.Доступность = НЕ ВариантРазбивкиНаЭтапы = "Процент";
	Элементы.ПроцентЭтапа.Видимость = ВариантРазбивкиНаЭтапы = "Процент"; 
	Элементы.ПроцентЭтапа.Доступность = ВариантРазбивкиНаЭтапы = "Процент";
КонецПроцедуры

&НаКлиенте
Процедура ВариантРазбивкиНаЭтапыПриИзменении(Элемент)
	ВидимостьВаринатовЭтапов();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ВариантРазбивкиНаЭтапы = "Количество";
	ВидимостьВаринатовЭтапов();
КонецПроцедуры

